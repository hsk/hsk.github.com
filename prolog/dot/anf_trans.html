<html>
<head>
<script src="http://pengines.swi-prolog.org/vendor/jquery/jquery-2.0.3.min.js"></script>
<script src="http://pengines.swi-prolog.org/pengine/pengines.js"></script>
<script type="text/x-prolog">
/*
anf_trans述語は、環境E, 項Tを受け取り穴(Hole)と変換式、対応する変数名を返します。holeは、継続式を置き換えるための入れ物で、束縛のないProlog
変数です。返却された項の中には、必ず穴が空いているので穴に変数を設定することで、継続の式を設定できます。
*/

:- op(920, xfx, [ ---> , -->> ]).
:- op(500, yfx, $).
:- op(1200, xfx, [ -- ]).
term_expansion(A -- B, B :- A).

v({_=_}).
v(_->_).

genid(E,E_,X) :- E_ is E + 1, format(atom(X),'.x~d',[E]).

anf_trans(E,X,E,Hole,X,Hole) :- atom(X).
anf_trans(E,(XT->T),E_,let(X=(XT->T_),Hole),X,Hole) :-
  genid(E,E1,X),
  anf_trans(E1,T,E_,T_,Y,Y).
anf_trans(E,V,E_,let(X=V,Hole),X,Hole) :- v(V), genid(E,E_,X).
anf_trans(E,S$T,E_,T_,Z,Hole):-
  genid(E,E1,Z),
  anf_trans(E1,S,E2,S_,X,let(Z=X$Y,Hole)),
  anf_trans(E2,T,E_,T_,Y,S_).
anf_trans(E,let(X=V,U),E_,let(X=V,U_),Y,Hole) :-
  v(V),
  anf_trans(E,U,E_,U_,Y,Hole).
anf_trans(E,let(X=T,U),E_,T_,Z,Hole) :-
  anf_trans(E,U,E1,U_,Z,Hole),
  anf_trans(E1,T,E_,T_,Y,let(X=Y,U_)).

pr(A) :- format(atom(B), '~w', [A]), pengine_output(B).
test_type :- v({'A'='S'}), v(x->x),pr('test_type ok').
test_variable :- anf_trans(0,x,0,x,X,X),pr('test_variable ok').
test_app :- anf_trans(0,x$y,_,A,Y,Y),pr('test_app ':A).
test_lambda :- anf_trans(0,(x->x)$(y->y),_,A,Y,Y), pr('test_lambda ':A).
test_let :- anf_trans(0,let(z=(x->x),z),_,A,Y,Y), pr('test_let ':A).
test_let2 :- anf_trans(0,let(z=(x->x)$(y->y),z),_,A,Y,Y), pr('test_let2 ':A).
main:-
  test_type,!,
  test_lambda,!,
  test_variable,!,
  test_app,!,
  test_let,!,
  test_let2,!,
  !
  ; pr('test error'),
  !.
</script>
<script>
var pengine = new Pengine({
ask:'main',
onoutput: function() {
  $('#out').html($('#out').html()+'<br/>'+JSON.stringify(this.data));
},
onerror: function() {
  $('#out').html($('#out').html()+'<br/>'+JSON.stringify(this.data));
},
server: 'http://pengines.swi-prolog.org/pengine'
});
</script>
</head>
<body>
A normal form translation tests.
<div id="out"></div>
</body>
</html>

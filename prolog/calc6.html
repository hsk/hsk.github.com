<html>
<head>
<script src="http://pengines.swi-prolog.org/vendor/jquery/jquery-2.0.3.min.js"></script>
<script src="http://pengines.swi-prolog.org/pengine/pengines.js"></script>
<script type="text/x-prolog">
/*
Small Step Evaluator with Evaluation Context using Prolog variable
*/
:- op(920, xfx, [ ---> , -->> ]).
:- op(1200, xfx, [ -- ]).
term_expansion(A -- B, B :- A).

v(A) :- integer(A).
e(T, H, T, H).
e(S+T, E+T, P, H) :- \+v(S), e(S,E,P,H).
e(S+T, T+E, P, H) :- v(S), \+v(T), e(T,E,P,H).
e(S-T, E-T, P, H) :- \+v(S), e(S,E,P,H).
e(S-T, S-E, P, H) :- v(S), \+v(T), e(T,E,P,H).
e(S*T, E*T, P, H) :- \+v(S), e(S,E,P,H).
e(S*T, S*E, P, H) :- v(S), \+v(T), e(T,E,P,H).
e(S/T, E/T, P, H) :- \+v(S), e(S,E,P,H).
e(S/T, S/E, P, H) :- v(S), \+v(T), e(T,E,P,H).

e(A, A_, T, P), A\=T, T ---> T_, P=T_
--%------------------------------------ (E-EvalContext)
A ---> A_.

integer(V1), integer(V2), V is V1 + V2
--%------------------------------------ (E-Plus3)
V1 + V2 ---> V.

integer(V1), integer(V2), V is V1 - V2
--%------------------------------------ (E-Minus3)
V1 - V2 ---> V.

integer(V1), integer(V2), V is V1 * V2
--%------------------------------------ (E-Times3)
V1 * V2 ---> V.

integer(V1), integer(V2), V is V1 div V2
--%------------------------------------ (E-Div3)
V1 / V2 ---> V.

A ---> A :- integer(A).               % (E-Int)

A -->> A_ :- \+v(A), A ---> B, !, B -->> A_.
A -->> A.

pr(A) :- format(atom(B), '~w', [A]), pengine_output(B).

main:-
  10 ---> R, pr(R),
  10 + 20 ---> R1, pr(R1),
  10 - 20 ---> R2, pr(R2),
  10 * 20 ---> R3, pr(R3),
  1*2+3/4 -->> R4, pr(R4),
  !.
</script>
<script>
var pengine = new Pengine({
ask:'main',
onoutput: function() {
  $('#out').html($('#out').html()+'<br/>'+JSON.stringify(this.data));
},
onerror: function() {
  $('#out').html($('#out').html()+'<br/>'+JSON.stringify(this.data));
},
server: 'http://pengines.swi-prolog.org/pengine'
});
</script>
</head>
<body>
Smallstep Evaluation Context
<div id="out"></div>
</body>
</html>
